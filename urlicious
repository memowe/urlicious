#!/usr/bin/env perl

use strict;
use warnings;

use Mojolicious::Lite;
use Mojo::JSON;

my $urlsfn  = app->home->rel_file('urls.json');
my $json    = Mojo::JSON->new;
my %urls    = ();

# load aliases from urls file
open my $fh, '<', $urlsfn or die "couldn't open $urlsfn: $!";
my $urls_json = join '' => <$fh>;
%urls = %{ $json->decode($urls_json) };

plugin charset => {charset => 'utf-8'};

get '/' => 'form';

post '/post' => sub {
    my $self = shift;

    my $url     = $self->param('url') // 'http://www.perl.org';
    my %alias   = reverse %urls;
    my $alias;

    # alias already exists
    if (exists $alias{$url}) {
        $alias = $alias{$url}
    }

    # alias needs to be created
    else {
    
        # generate a new alias: four characters of [a-zA-Z0-9]
        my @chars = ( 'a' .. 'z', 'A' .. 'Z', 0 .. 9 );
        do {
            $alias = join '' => map {$chars[rand @chars]} 1 .. 4;
        } while exists $urls{$alias};

        # update %urls
        $urls{$alias} = $url;

        # write to file
        open my $fh, '>', $urlsfn or die "couldn't write $urlsfn: $!";
        print $fh $json->encode(\%urls);
    }

    # show generated alias
    $self->stash(url => $url, alias => $alias);
};

get '/:alias' => sub {
    my $self = shift;

    # try to lookup
    my $url = $urls{$self->param('alias')};

    # alias unknown
    $self->render_not_found and return unless $url;

    # alias exists: redirect
    $self->redirect_to($url);
};

app->start;
__DATA__

@@ form.html.ep
% layout 'outer';
<form action="/post" method="post">
<fieldset><legend>Get a smaller URL!</legend>
<p>
    <label for="url">Your fat URL</label>:
    <input type="text" name="url" id="url">
    <input type="submit" value="make it shorter!">
</p>
</fieldset>

@@ post.html.ep
% layout 'outer';
% ( my $texturl = $url ) =~ s/^(.{42}).+$/$1.../;
% my $newurl    = $self->req->url->base . "/$alias";
% my $ratio     = length($newurl) / length($url);
% my $shorterp  = ( 1 - $ratio ) * 100;
<p>Done. We created an alias for your URL:</p>
<table>
    <tr>
        <th>Your URL:</th>
        <td><a href="<%= $url %>"><%= $texturl %></a></td>
    </tr>
    <tr>
        <th>The new alias:</th>
        <td>
            <a href="<%= $newurl %>"><%= $newurl %></a>
            (<%= int($shorterp) %>% shorter)
        </td>
    </tr>
</table>

@@ layouts/outer.html.ep
<!doctype html>
<html>
<head>
<title>Urlicious!</title>
<link rel="stylesheet" type="text/css" href="<%= url_for 'style.css' %>">
</head>
<body>
<div id="content">
<h1><a href="/">urlicious!</a></h1>
%== content;
</div>
</body>
</html>

@@ style.css
html, body { margin: 0; padding: 0; font-family: sans-serif; line-height: 150% }
body { color: black; background-color: #bbc }
#content { width: 76ex; color: black; background-color: #f0f0f8; margin: 0 auto 1em; padding: 4.5em 6ex 4.5em; border: solid #99a; border-width: 0 thick thick }
h1 { font-family: 'Trebuchet MS', sans-serif; font-weight: bold; font-size: 4em; margin: 0 0 .5em; padding: 0 0 .5em; border-bottom: thick solid #d0d0e0; text-align: center }
h1 a { text-decoration: none; color: black !IMPORTANT }
fieldset { border: medium solid #d0d0e0; background-color: white; color: black; padding: 1em 4ex }
legend { background-color: #d0d0e0; color: black; padding: .2em 2ex; border-radius: 1em }
input[type=text] { width: 20ex; padding: .2em .5ex; font-size: 1em; color: #666; background-color: #d0d0e0; margin: 0 1em; border: thin solid black }
input[type=text]:focus { color: black; background-color: #f0f0f8 }
input[type=submit] { font-size: 1em }
table { color: black; background-color: white }
th, td { padding: .5em 3ex .5em 0; font-weight: normal }
th { padding-left: 3ex; text-align: right }
a { color: black; background-color: transparent }
a:visited { color: #666; background-color: transparent }
